import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

repositories {
    mavenCentral()
    maven { url 'https://repo.viaversion.com/' }
    maven { url = "https://repo.aikar.co/nexus/content/groups/aikar/" }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    api project(':API')
    api(adventureDependencies)

    implementation "co.aikar:minecraft-timings:1.0.4"

    compileOnly "io.netty:netty-all:${nettyVersion}"
    compileOnly 'com.viaversion:viaversion:4.5.0'

    compileOnly 'org.spigotmc:spigot-api:1.19.3-R0.1-SNAPSHOT'

    testCompileOnly 'org.spigotmc:spigot-api:1.19.3-R0.1-SNAPSHOT'
}

test {
    useJUnitPlatform()
}

shadowJar {
    relocate 'co.aikar.timings.lib', 'de.snowii.packeto.timingslib'
}

tasks.register('testJar', ShadowJar) {
    classifier = 'tests'
    from(shadowJar)
    from sourceSets.main.output
    from sourceSets.test.output
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

processResources {
    def props = [name: name, version: version, description: description]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

processTestResources {
    def props = [name: name, version: version, description: description]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
